// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU-nostat.
// Author: Benjamin Gabay

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not i.e. 1's complement
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not i.e. 1's complement
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not i.e. 1's complement

CHIP ALU-nostat {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // bitwise not (1's complement) the x input?
        zy, // zero the y input?
        ny, // bitwise not (1's complement) the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // bitwise not (1's complement) the out output?

    OUT 
        out[16]; // 16-bit output

    PARTS:        
	//Zero out x or y if zx or zy it true respectively
	Mux16(a=x, b=false, sel=zx, out=x1);
	Mux16(a=y, b=false, sel=zy, out=y1);
	
	//Compliment (Not) x and y if nx or ny bit is true respectively
	Not16(in=x1, out=x1Not);
	Mux16(a=x1, b=x1Not, sel=nx, out=x2);
	Not16(in=y1, out=y1Not);
	Mux16(a=y1, b=y1Not, sel=ny, out=y2);
	
	//Set output to either the AND or the ADDITION of x and y depending on the value of f
	And16(a=x2, b=y2, out=xANDy);
	Add16(a=x2, b=y2, out=xPLUSy);
	Mux16(a=xANDy, b=xPLUSy, sel=f, out=xy);
	
	//Compliment the output before setting to out if no bit is true
	Not16(in=xy, out=xyNot);
	Mux16(a=xy, b=xyNot, sel=no, out=out);
}